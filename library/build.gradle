plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 56
        versionName '1.6.8'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        debug {
            debuggable true
            zipAlignEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        dataBinding true
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations {
    include
}

project.afterEvaluate {
    task copyDeps(type: Copy) {
        from configurations.include {
            include '**/*.jar'
        }
        into './build/intermediates/aar_libs_directory/release/libs'
    }
    mergeReleaseJniLibFolders.dependsOn copyDeps

    task copyDebugDeps(type: Copy) {
        from configurations.include {
            include '**/*.jar'
        }
        into './build/intermediates/aar_libs_directory/debug/libs'
    }
    mergeDebugJniLibFolders.dependsOn copyDebugDeps
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation project(':annotations')
    include project(':annotations')

    api 'androidx.appcompat:appcompat:1.3.0'
    api 'androidx.constraintlayout:constraintlayout:2.0.4'
    api 'com.google.android.material:material:1.3.0'
    api 'com.google.code.gson:gson:2.8.7'
    api 'com.wdullaer:materialdatetimepicker:4.2.3'
    api 'com.github.esafirm.android-image-picker:imagepicker:2.4.5'
    api 'com.theartofdev.edmodo:android-image-cropper:2.8.0'
    api 'io.reactivex.rxjava3:rxjava:3.0.13'
    api 'io.reactivex.rxjava3:rxandroid:3.0.0'
    api 'net.danlew:android.joda:2.10.9.1'
}

publishing {
    task sourceJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier 'sources'
    }
    publications {
        library(MavenPublication) {
            artifact sourceJar
            artifact "$buildDir/outputs/aar/library-release.aar"
            groupId 'com.nextstory'
            artifactId 'library'
            version android.defaultConfig.versionName

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.api.allDependencies.each {
                    if (!it.group.contains('androidx.databinding')) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "$rootDir/repository"
        }
    }
}
